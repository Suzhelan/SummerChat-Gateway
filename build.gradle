plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.0'
    id 'io.spring.dependency-management' version '1.1.5'
}


subprojects {

    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "java"

    group = 'top.ocsc'
    version = '0.0.1-SNAPSHOT'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    repositories {
        mavenCentral()
    }

    ext {
        springCloudVersion = "2023.0.2"
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
        }
    }

    configurations {
        // 排除spring自带的日志框架,以免产生冲突
        all*.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }

    dependencies {
        //implementation 'org.springframework.boot:spring-boot-starter-web'//导入了gateway就不用web的starter了
        implementation 'org.springframework.cloud:spring-cloud-starter-bootstrap'
        //nacos
        implementation 'com.alibaba.cloud:spring-cloud-starter-alibaba-nacos-discovery:2023.0.1.0'
        implementation 'com.alibaba.cloud:spring-cloud-starter-alibaba-nacos-config:2023.0.1.0'
        //api网关
        implementation 'org.springframework.cloud:spring-cloud-starter-gateway'
        //json
        implementation 'com.alibaba.fastjson2:fastjson2:2.0.51'
        //网关层限流
        implementation 'org.springframework.boot:spring-boot-starter-data-redis-reactive'

        //log4j2
        implementation 'org.springframework.boot:spring-boot-starter-log4j2'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    bootJar {
        enabled = false
    }
    jar {
        enabled = true
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

